plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springdoc.openapi-gradle-plugin' version '1.8.0'
    id 'jacoco'
}

group = 'sg.wlian'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

openApi {
    apiDocsUrl.set("http://localhost:5456/api-docs")
    outputDir.set(file("./build/generated/openapi"))
    outputFileName.set("openapi.json")
    customBootRun {
        args.set(["--spring.profiles.active=dev", "--server.port=5456"])
    }
}

tasks.register('cleanDocs', Delete) {
    delete fileTree("$rootDir/build/generated/openapi") {
        include '**/*'
    }
}

tasks.named('generateOpenApiDocs') {
    dependsOn 'cleanDocs'
}


dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework.data:spring-data-rest-hal-explorer'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    runtimeOnly 'com.mysql:mysql-connector-j'


    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.hamcrest:hamcrest'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/html')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}
